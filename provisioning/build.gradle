apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

android {
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 31
        versionName "2.4.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.espressif.provisioning'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.9'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'com.google.protobuf:protobuf-javalite:3.18.0'
    implementation 'com.google.crypto.tink:tink-android:1.12.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.2'

    // CameraX
    def camerax_version = "1.5.1"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    // ML Kit for QR code scanning
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

// Maven publishing configuration for JitPack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.KhushbuShah25'
                artifactId = 'provisioning'
                version = android.defaultConfig.versionName

                // Publish the release AAR
                artifact(tasks.named("bundleReleaseAar"))

                pom {
                    name = 'ESP Provisioning Library'
                    description = 'ESP Device Provisioning Library for Android'
                    url = 'https://github.com/KhushbuShah25/esp-idf-provisioning-android'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'espressif'
                            name = 'Espressif Systems'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/KhushbuShah25/esp-idf-provisioning-android.git'
                        developerConnection = 'scm:git:ssh://github.com/KhushbuShah25/esp-idf-provisioning-android.git'
                        url = 'https://github.com/KhushbuShah25/esp-idf-provisioning-android'
                    }

                    // Add dependencies to POM
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        configurations.api.allDependencies.each {
                            if (it.group != null && it.name != null) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                        configurations.implementation.allDependencies.each {
                            if (it.group != null && it.name != null) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }
    }
}
